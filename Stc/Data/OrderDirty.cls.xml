<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Stc.Data.OrderDirty">
<Description>
Заявка от заказчика новая и не одобренная</Description>
<ClassType>persistent</ClassType>
<Super>Stc.Data.Order</Super>
<TimeCreated>63098,13699.29894</TimeCreated>

<Property name="FirstName">
<Description>
Контактное лицо, Имя</Description>
<Type>Stc.Data.Localization.LText</Type>
<Required>1</Required>
</Property>

<Property name="LastName">
<Description>
Контактное лицо, Фамилия</Description>
<Type>Stc.Data.Localization.LText</Type>
<Required>1</Required>
</Property>

<Property name="MiddleName">
<Description>
Контактное лицо, Отчество</Description>
<Type>Stc.Data.Localization.LText</Type>
<Required>1</Required>
</Property>

<Property name="Email">
<Description>
Контактное лицо, Почта</Description>
<Type>%String</Type>
<Required>1</Required>
<Parameter name="MAXLEN" value="500"/>
</Property>

<Property name="Phone">
<Description>
Контактное лицо, Телефон</Description>
<Type>%String</Type>
<Parameter name="MAXLEN" value="500"/>
</Property>

<Property name="Skype">
<Description>
Контактное лицо, Скайп</Description>
<Type>%String</Type>
<Parameter name="MAXLEN" value="500"/>
</Property>

<Property name="CompanyString">
<Description>
Короткое название новой компании</Description>
<Type>%String</Type>
<Parameter name="MAXLEN" value="500"/>
</Property>

<Property name="CompanySite">
<Description>
Сайт компании</Description>
<Type>%String</Type>
<Parameter name="MAXLEN" value="500"/>
</Property>

<Property name="StudentsNumber">
<Description>
Количество слушателей</Description>
<Type>%Integer</Type>
<Required>1</Required>
</Property>

<Method name="ConvertToProxyObject">
<Description>
Конвертация в proxyObject для JSON </Description>
<ReturnType>%ZEN.proxyObject</ReturnType>
<Implementation><![CDATA[
	set proxy = ##class(%ZEN.proxyObject).%New()
	
	set proxy.id = ..%Id()
	set proxy.status = ..OrderStatus.ConvertToProxyObject()
	set proxy.city = ..City.ConvertToProxyObject()
	set proxy.course = ..Course.ConvertToProxyObject()
	
	set proxy.contact = ##class(%ZEN.proxyObject).%New()
	set proxy.contact.lastName = ..LastName.Value
	set proxy.contact.firstName = ..FirstName.Value
	set proxy.contact.middleName = ..MiddleName.Value
	set proxy.contact.email = ..Email
	set proxy.contact.phone = ..Phone
	set proxy.contact.skype = ..Skype
	set proxy.contact.partner = ..Partner
	
	set proxy.contact.company = ##class(%ZEN.proxyObject).%New()

	if (..Company) {
		set proxy.contact.company = ..Company.ConvertToProxyObject(1)
		set proxy.contact.company.contact = ..Company.Contact.ConvertToProxyObject()
	}
	else{
		set proxy.contact.company.shortName = ..CompanyString
		set proxy.contact.company.site = ..CompanySite	
	}
	
	set proxy.date = $ZDATE(..TrainingDate, 3)
	set proxy.createdTS = ..CreatedTS
	set proxy.studentsNumber = ..StudentsNumber
	
	quit proxy
]]></Implementation>
</Method>

<Method name="Create">
<Description>
Создание 'новой' заявки</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[data:%RegisteredObject,&status]]></FormalSpec>
<ReturnType>OrderDirty</ReturnType>
<Implementation><![CDATA[
	set order = ..%New()
	
	set order.City = ##class(Region).%OpenId(data.city.id)
	set order.Course = ##class(Course).%OpenId(data.course.id)
		
	set order.FirstName = ##class(Stc.Data.Localization.LText).Create($ZCONVERT(data.contact.firstName, "I","UTF8"), "Stc.Data.Localization.Translation;PersonLTextValue")
	set order.LastName = ##class(Stc.Data.Localization.LText).Create($ZCONVERT(data.contact.lastName, "I","UTF8"), "Stc.Data.Localization.Translation;PersonLTextValue")
	set order.MiddleName = ##class(Stc.Data.Localization.LText).Create($ZCONVERT(data.contact.middleName, "I","UTF8"), "Stc.Data.Localization.Translation;PersonLTextValue")
		
	set order.TrainingDate = $ZDATEH(data.date, 15)
	set order.StudentsNumber = data.studentsNumber
	set order.Email = $ZCONVERT(data.contact.email, "I","UTF8")
	set order.Phone = $ZCONVERT(data.contact.phone, "I","UTF8")
	set order.Skype = $ZCONVERT(data.contact.skype, "I","UTF8")
	set order.Partner = $ZCONVERT(data.contact.partner, "I","UTF8")
		
	set order.Longitude = $p(data.point, " ", 1)
	set order.Latitude = $p(data.point, " ", 2)
		
	if (data.contact.company.exist) set order.Company = ##class(Company).%OpenId(data.contact.company.exist.id)
		
	if '(order.Company){
		// Если компания не была выбрана в заявке, то поиск по имени существующей,
		// нашли - устаналиваем, нет - новая компания -> строка
		set compName = $ZCONVERT(data.contact.company.notexist.name, "I","UTF8")
		set compId = ""
		&sql(SELECT ID INTO:compId FROM Stc_Data.Company WHERE ShortName->Value  = :compName OR FullName->Value = :compName)
		if (##class(Stc.Data.Company).%ExistsId(compId) = 0) {
			set order.CompanyString = $ZCONVERT(data.contact.company.notexist.name, "I","UTF8")
			set order.CompanySite = $REPLACE($REPLACE($ZCONVERT(data.contact.company.notexist.site, "I","UTF8"),"http://",""), "https://","")
		}
		else{
			set order.Company = ##class(Stc.Data.Company).%OpenId(compId,3,.status)
		}
	} 

	set order.CreatedTS = $ZDATETIME($NOW(), 3)
	set order.OrderStatus = ##class(OrderStatus).CodeIdxOpen("New")
		
	set status = order.%Save()
		
	quit order
]]></Implementation>
</Method>

<Storage name="Default">
<Type>%Library.CacheStorage</Type>
<DefaultData>OrderDirtyDefaultData</DefaultData>
<Data name="OrderDirtyDefaultData">
<Subscript>"OrderDirty"</Subscript>
<Value name="1">
<Value>ContactName</Value>
</Value>
<Value name="2">
<Value>Company</Value>
</Value>
<Value name="3">
<Value>CompanyString</Value>
</Value>
<Value name="4">
<Value>Contact</Value>
</Value>
<Value name="5">
<Value>NumberStudents</Value>
</Value>
<Value name="6">
<Value>FirstName</Value>
</Value>
<Value name="7">
<Value>LastName</Value>
</Value>
<Value name="8">
<Value>MiddleName</Value>
</Value>
<Value name="9">
<Value>Email</Value>
</Value>
<Value name="10">
<Value>Phone</Value>
</Value>
<Value name="11">
<Value>Skype</Value>
</Value>
<Value name="12">
<Value>Partner</Value>
</Value>
<Value name="13">
<Value>CompanySite</Value>
</Value>
<Value name="14">
<Value>StudentsNumber</Value>
</Value>
</Data>
</Storage>
</Class>
</Export>
